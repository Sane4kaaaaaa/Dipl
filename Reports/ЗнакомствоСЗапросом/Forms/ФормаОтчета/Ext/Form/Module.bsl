
&НаКлиенте
Процедура КомандаВыполнитьЗапрос(Команда)
	// 1 - Проверка и форматирование введенного текста
	     НеотформатированныйТекст= ТекстЗапроса.ПолучитьТекст();
		 ОтформатированныйТекст= СтрЗаменить(НеотформатированныйТекст, "|","");
		 
		 Если ПустаяСтрока(ОтформатированныйТекст) Тогда
			 ТекстПредупреждения = НСтр("ru= 'Введите текст запроса.'");
			 ПоказатьПредупреждение(, ТекстПредупреждения);
			 Возврат;
		 КонецЕсли;
		 
		 ТекстЗапроса.УстановитьТекст(ОтформатированныйТекст);
		 
	//2 - Очистка табличного документа "РезультатЗапроса" в форме   
	     РезультатВыполненияЗапроса = Новый ТабличныйДокумент;
		 
	//3 - Выполняем запрос (на сервере) и выводим результаты
		ВыполнитьЗапрос(ТекстЗапроса.ПолучитьТекст());
		
	//4 - Перейти на вкладку с результатами (актуально, если находимая на вкладке "Параметры")
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.Запрос;
КонецПроцедуры    

&НаСервере   
Процедура ВыполнитьЗапрос(ПолученныйТекстЗапроса)
	
//1 - Запрос данных	
	Запрос = Новый Запрос; 
	Запрос.Текст = ПолученныйТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТЧ",Отчет.ТЧ_ТабличнаяЧасть.Выгрузить(,"ТЧ_Ссылка"));  
	Запрос.УстановитьПараметр("НачалоПериода",Отчет.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",Отчет.КонецПериода);
	Запрос.УстановитьПараметр("ЗначениеСсылка",Отчет.ЗначениеСсылка);
    Запрос.УстановитьПараметр("ЗначениеЧисло",Отчет.ЗначениеЧисло);  
	Запрос.УстановитьПараметр("ЗначениеБулево",Отчет.ЗначениеБулево);
    Запрос.УстановитьПараметр("ЗначениеСтрока",Отчет.ЗначениеСтрока);     
	
	РезультатЗапроса = Запрос.Выполнить();
	РезультатЗапросаТЗ = РезультатЗапроса.Выгрузить();//[Таблица Значений]
	
//2 - Формирование табличного документа с результатами запроса
	ТабДок = РезультатВыполненияЗапроса;
	ОбластьШапкаТаблицы = ТабДок.ПолучитьОбласть(1,1,1, РезультатЗапросаТЗ.Колонки.Количество());
	ОбластьДанныеТаблицы = ТабДок.ПолучитьОбласть(2,1,2, РезультатЗапросаТЗ.Колонки.Количество());
	
	ЗаголовкиКолонокТаблицы = Новый Структура;
	
//2.1 - Форматирование областей вывода шапки и данных таблицы
	ЛинияОбводки = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ШрифтЗаголовковТаблицы = Новый Шрифт(, 8, Истина, , , );
	ШрифтДанныхТаблицы = Новый Шрифт(, 8, , , , );
	
	ЦветфонаШапкиТаблицы = Новый Цвет(255,255,153);  
	ЦветфонаДанныхТаблицы = Новый Цвет(245,255,250);  
	
	Для Счетчик = 1 по РезультатЗапросаТЗ.Колонки.Количество() Цикл
		
		Колонка = РезультатЗапросаТЗ.Колонки[Счетчик-1];
		ЗаголовкиКолонокТаблицы.Вставить(Колонка.Имя, Колонка.Имя);
		
		ОбластьЗаполенияШапки = ОбластьШапкаТаблицы.Область(1, Счетчик, 1, Счетчик); 
		ОбластьЗаполенияШапки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьЗаполенияШапки.ШиринаКолонки = 15;
		ОбластьЗаполенияШапки.Обвести(ЛинияОбводки, ЛинияОбводки, ЛинияОбводки, ЛинияОбводки);
		ОбластьЗаполенияШапки.Параметр = Колонка.Имя;
		ОбластьЗаполенияШапки.Шрифт = ШрифтЗаголовковТаблицы;
		ОбластьЗаполенияШапки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьЗаполенияШапки.ЦветФона = ЦветФонаШапкиТаблицы;   
		
		ОбластьЗаполенияДанных = ОбластьДанныеТаблицы.Область(1, Счетчик, 1, Счетчик); 
		ОбластьЗаполенияДанных.Параметр = Колонка.Имя;       
		ОбластьЗаполенияДанных.ШиринаКолонки = 15;  
		ОбластьЗаполенияДанных.Шрифт = ШрифтДанныхТаблицы;   
		ОбластьЗаполенияДанных.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
		ОбластьЗаполенияДанных.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		ОбластьЗаполенияДанных.Обвести(ЛинияОбводки, ЛинияОбводки, ЛинияОбводки, ЛинияОбводки);
		ОбластьЗаполенияДанных.ЦветФона = ЦветФонаДанныхТаблицы;   
		
	КонецЦикла;   

//2.2 - Вывод шапки таблицы
	ОбластьШапкаТаблицы.Параметры.Заполнить(ЗаголовкиКолонокТаблицы);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
//2.3 - Вывод данных таблицы
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьДанныеТаблицы.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьДанныеТаблицы);
	КонецЦикла;
	
КонецПроцедуры




	
	
